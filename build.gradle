// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.novoda:bintray-release:0.9.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    tasks.withType(Javadoc) {
        options {
            encoding "UTF-8"
            charSet 'UTF-8'
            links "http://docs.oracle.com/javase/7/docs/api"
        }
//        exclude(
//                "**/ClassicsAbstract.java",
//                "**/ClassicsHeader.java",
//                "**/PathsDrawable.java",
//                "**/BallPulseFooter.java",
//                "**/BezierRadarHeader.java",
//                "**/TwoLevelHeader.java"
//        )
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    if (name.startsWith("refresh-")) {

        apply plugin: 'maven'

        group = SMART_GROUP
        version = SMART_VERSION

        def artifact = name

        uploadArchives {
            repositories.mavenDeployer {
                repository(url: uri('../repo'))
                pom.groupId = SMART_GROUP
                pom.artifactId = artifact
                pom.version = SMART_VERSION
            }
        }

        afterEvaluate {
            if (description != null) {
                apply plugin: 'com.novoda.bintray-release'
                publish {
                    userOrg = SMART_USER
                    groupId = SMART_GROUP
                    artifactId = artifact
                    publishVersion = SMART_VERSION
                    desc = description
                    website = "https://github.com/scwang90/${rootProject.name}"
                }
            }
        }

    }

}
task clean(type: Delete) {
    delete rootProject.buildDir
}

